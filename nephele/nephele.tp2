// --------------------------------------------
// Mod setup
// --------------------------------------------
// Basics
BACKUP ~nephele/backup~
//AUTHOR ~K'aeloree (kae@spellholdstudios.net)~
SUPPORT ~http://www.shsforums.net/forum/608-nephele/~
VERSION ~v2.4~
README ~nephele/nephele_readme.html~

ALWAYS

  ACTION_IF NOT VARIABLE_IS_SET bg2_chapter THEN BEGIN // check to make this happen only once per install

/* not really necessary because no chapter variable used but I'll put this here nevertheless */
    ACTION_IF GAME_IS ~eet~ BEGIN
      OUTER_SET bg2_chapter = 12
    END ELSE BEGIN
      OUTER_SET bg2_chapter = 0
    END
    OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
      OUTER_SET bg2_chapter = bg2_chapter + 1
      OUTER_SPRINT name_source ~bg2_chapter_%i%~
      OUTER_SET EVAL ~%name_source%~ = bg2_chapter
    END

    // convert strings to UTF-8 for BGEE/BG2EE
    ACTION_DEFINE_ARRAY lk#reload BEGIN setup END
    LAF HANDLE_CHARSETS 
	INT_VAR infer_charsets = 1 
	STR_VAR tra_path = ~nephele/tra~
	reload_array = lk#reload 
    END

  END

END

AUTO_TRA ~nephele/tra/%s~

LANGUAGE ~English~
         ~english~
         ~nephele/tra/english/setup.tra~


// --------------------------------------------
// Nephele NPC Mod 
// --------------------------------------------
// Component setup
BEGIN @1
  REQUIRE_PREDICATE GAME_IS ~bg2ee bgt tob eet~ @2


// Adds custom IsValidForPartyDialogue state used throughout nephele
APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

ACTION_IF GAME_IS ~tob bgt~ THEN BEGIN
  // Fixing Imoen's dialogue file
  ACTION_IF GAME_IS ~tob~ THEN BEGIN
      COPY_EXISTING ~interdia.2da~ ~override~
       READ_2DA_ENTRY 17 0 2 row17DV
       PATCH_IF (~%row17DV%~ STR_EQ ~IMOEN~) THEN BEGIN
       SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
       END
      BUT_ONLY_IF_IT_CHANGES
  END ELSE BEGIN
    APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ // fixes SoA version
      UNLESS ~BIMOEN2~
  END

  // Fixpack ids fixes
  INCLUDE ~nephele/lib/ids.tph~
  INCLUDE ~nephele/lib/times.tph~
  CLEAR_IDS_MAP
END

// Kit
//COPY ~nephele\2da\lkneph.2da~ ~override~


ADD_KIT ~lkneph~
~LK#Neph 0 0 0 0 0 0 0 0~ 
~LK#Neph 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
~LK#Neph 0 0 0 0 9 0~ 
~LK#Neph 0 0 0 0 0 0~ 
~LK#Neph 0 0 0 0 0 0~ 
~LK#Neph 0 0 0 0 0 0~
~LK#Neph 1 1 0 1 0 0 0 0 0~ 
~LK#Neph 0 0 0 0 0 0~ 
~nephele\2da\lkneph.2da~
~~
~0x00100000 3~
~Cl0~
~PLAT04 * HELM07 BAG20 RING07 RING31 * BOOT01 AMUL20 BRAC09 BELT04 SW1H31 CLCK07 SHLD24 BAG02 BAG04 * * * *~ 
SAY @3
SAY @3 
SAY @4

/*
ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN

  INCLUDE ~nephele/lib/cd_ee_kit_appends.tph~
  LAUNCH_ACTION_FUNCTION ee_kit_appends STR_VAR class = "CLERIC" kitname = "lkneph" END

END
*/
INCLUDE ~nephele/lib/fl#add_kit_ee.tpa~
LAF fl#add_kit_ee STR_VAR kit_name = lkneph END

// Copying images and creatures
ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
  COPY ~nephele/images/ee~ ~override~
END ELSE BEGIN
  COPY ~nephele/images/bg2~ ~override~
END

COPY ~nephele/creatures/lk#neph.cre~ ~override/lk#neph.cre~
  SAY NAME1 @5
  SAY NAME2 @6
  WRITE_EVALUATED_ASCII DIALOG          ~%DEST_RES%~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~Nephele~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~%DEST_RES%~ #8
  WRITE_LONG 0x18 190000
  WRITE_SHORT 0x244 0
  WRITE_SHORT 0x246 0x4000 + ~%lkneph%~
  ADD_CRE_ITEM ~slng02~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~bull02~ #20 #0 #0 ~IDENTIFIED~ ~QUIVER1~
  SAY BIO @7
  SAY MORALE @8
  SAY LEADER @9
  SAY TIRED @10
  SAY BORED @11
  SAY BATTLE_CRY1 @12
  SAY BATTLE_CRY2 @13
  SAY BATTLE_CRY3 @14
  SAY BATTLE_CRY4 @15
  SAY BATTLE_CRY5 @16
  SAY DYING @17
  SAY HURT @18
  SAY AREA_DUNGEON @19
  SAY SELECT_COMMON1 @20
  SAY SELECT_COMMON2 @21
  SAY SELECT_COMMON3 @22
  SAY SELECT_COMMON4 @23
  SAY SELECT_COMMON5 @24
  SAY SELECT_COMMON6 @25
  SAY SELECT_ACTION1 @26
  SAY SELECT_ACTION2 @27
  SAY SELECT_ACTION3 @28
  SAY SELECT_ACTION4 @29
  SAY SELECT_ACTION5 @30
  SAY SELECT_ACTION6 @31
  SAY SELECT_ACTION7 @32
  SAY SELECT_RARE1 @33
  SAY SELECT_RARE2 @34
  SAY CRITICAL_HIT @35
  SAY CRITICAL_MISS @36
  SAY TARGET_IMMUNE @37
  SAY INVENTORY_FULL @38
  SAY HAPPY @39
  SAY UNHAPPY_ANNOYED @40
  SAY UNHAPPY_SERIOUS @41
  SAY SPELL_DISRUPTED @42
  SAY SET_A_TRAP @43
  SAY HIDDEN_IN_SHADOWS @44
  SAY PICKED_POCKET @45


COPY ~nephele/creatures/lk#neph.cre~ ~override/lk#ne25.cre~
  SAY NAME1 @5
  SAY NAME2 @6
  WRITE_EVALUATED_ASCII DIALOG          ~%DEST_RES%~ #8
  WRITE_EVALUATED_ASCII DEATHVAR        ~Nephele~ #32
  WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE ~%DEST_RES%~ #8
  WRITE_LONG 0x18 2500000
  WRITE_SHORT 0x244 0
  WRITE_SHORT 0x246 0x4000 + ~%lkneph%~
  ADD_CRE_ITEM ~slng04~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~bull05~ #20 #0 #0 ~IDENTIFIED~ ~QUIVER1~
  SAY BIO @7
  SAY MORALE @8
  SAY LEADER @9
  SAY TIRED @10
  SAY BORED @11
  SAY BATTLE_CRY1 @12
  SAY BATTLE_CRY2 @13
  SAY BATTLE_CRY3 @14
  SAY BATTLE_CRY4 @15
  SAY BATTLE_CRY5 @46
  SAY DYING @17
  SAY HURT @47
  SAY AREA_DUNGEON @19
  SAY SELECT_COMMON1 @20
  SAY SELECT_COMMON2 @21
  SAY SELECT_COMMON3 @22
  SAY SELECT_COMMON4 @23
  SAY SELECT_COMMON5 @24
  SAY SELECT_COMMON6 @25
  SAY SELECT_ACTION1 @26
  SAY SELECT_ACTION2 @27
  SAY SELECT_ACTION3 @28
  SAY SELECT_ACTION4 @29
  SAY SELECT_ACTION5 @30
  SAY SELECT_ACTION6 @31
  SAY SELECT_ACTION7 @32
  SAY SELECT_RARE1 @33
  SAY SELECT_RARE2 @34
  SAY CRITICAL_HIT @35
  SAY CRITICAL_MISS @36
  SAY TARGET_IMMUNE @37
  SAY INVENTORY_FULL @38
  SAY HAPPY @39
  SAY UNHAPPY_ANNOYED @40
  SAY UNHAPPY_SERIOUS @41
  SAY SPELL_DISRUPTED @42
  SAY SET_A_TRAP @43
  SAY HIDDEN_IN_SHADOWS @44
  SAY PICKED_POCKET @45





// Epilogues
COPY ~nephele/epilogues/lk#neph1.2da~ ~override/lk#neph1.2da~
   REPLACE ~99999~ @48

COPY ~nephele/epilogues/lk#neph1.2da~ ~override/lk#neph2.2da~
   REPLACE ~99999~ @49


// Compiling dialogue...
COMPILE ~nephele/dialogue/lk#neph.d~
COMPILE EVALUATE_BUFFER ~nephele/dialogue/lk#nepj.d~
COMPILE EVALUATE_BUFFER ~nephele/dialogue/blk#nep.d~
COMPILE ~nephele/dialogue/lk#nepp.d~
COMPILE ~nephele/dialogue/lk#ne25.d~
COMPILE EVALUATE_BUFFER ~nephele/dialogue/lk#ne25j.d~
COMPILE ~nephele/dialogue/blk#ne25.d~
COMPILE ~nephele/dialogue/lk#ne25p.d~
//COMPILE ~nephele/dialogue/fatesp.d~ moved to above with EET separation


// Compiling scripts...
COMPILE EVALUATE_BUFFER ~nephele/scripts/lk#neph.baf~
COMPILE ~nephele/scripts/lk#nepd.baf~
COMPILE ~nephele/scripts/lk#ne25.baf~
EXTEND_BOTTOM ~ar2010.bcs~ ~nephele/scripts/ar2010.baf~
EXTEND_TOP ~ar6200.bcs~ ~nephele/scripts/ar6200.baf~


// Adding talk condition to Yoshimo just in case...
COPY_EXISTING ~yoshimox.dlg~ ~override~
  DECOMPILE_DLG_TO_D
    REPLACE_TEXTUALLY ~Enemy()~ ~SetGlobal("LK#NephYoshBetray","GLOBAL",1) Enemy()~
  COMPILE_D_TO_DLG
  BUT_ONLY_IF_IT_CHANGES

// Appends
APPEND ~interdia.2da~ ~Nephele BLK#NEP BLK#NE25~
   UNLESS ~Nephele~

APPEND ~pdialog.2da~ ~Nephele LK#NEPP LK#NEPJ LK#NEPD LK#NE25P LK#NE25J LK#NE25D LK#NE25~
  UNLESS ~Nephele~

ACTION_IF GAME_IS ~eet~ THEN BEGIN
  INCLUDE ~EET/other/EET_functions.tph~

  LAF ~EET_NPC_TRANSITION~
    INT_VAR
      type = 2 // BG2 NPC without BG1 content (transition to ToB if joined party at least once in SoA)
      default_ToB = 1 //controls if NPC should be spawnable in ToB when the game has been started in ToB (no by default - 0)
    STR_VAR
      dv = "NEPHELE" //Death Variable (script name) of the NPC (must be the same in BG1/SoD/BG2/ToB)
      override_SoA = "LK#NEPH" //name of the override NPC script in SoA
      override_ToB = "LK#NE25" //name of the override NPC script in ToB
      dialog_ToB = "LK#NE25" //name of the dialogue file used after spawning NPC in ToB
	  SummonedVar = "LK#NepheleSummoned" // global that will be used to track if Nephele has been summoned (used by EET Fate Spirit code, also used by lk#ne25.baf to trigger initial dialogue with PC)
	  SpawnPlaneVar = "LK#NepheleSummoned" // global that will be used to track if Nephele has been summoned (used by EET Fate Spirit code, also used by lk#ne25.baf to trigger initial dialogue with PC)
	  cre_ToB = "LK#NE25" // if Nephele was not in the party before ToB, this CRE will be created when she is summoned by Fate Spirit
      traFile = EVAL "nephele/tra/%LANGUAGE%/fatesp.tra" // TRA file that will be used for the TRA references defined in "string"
      string = "@0" // FATESP summon text (example: "Bring me Quayle, the gnome cleric/illusionist.") or TRA string number
      stringPosDV = "Quayle" //set the DV of NPC right below your response (used for sorting FATESP responses alphabetically)
      //Aerie, Ajantis, Alora, Anomen, Baeloth, Branwen, Cernd, Coran, Corwin, Dorn, Dynaheir, Edwin, Eldoth, Faldorn, Garrick,
      //Glint, HaerDalis, Hexxat, Imoen2, Jaheira, Jan, Kagain, Keldorn, Khalid, Kivan, Korgan, MKhiin, Mazzy, Minsc, Montaron,
      //Nalia, Neera, Quayle, Rasaad, Safana, SharTeel, Skie, Tiax, Viconia, Valygar, Voghiln, Wilson, Xan, Xzar, Yeslick, Yoshimo
      //variable not set (default) = NPC name appended at the end of summoning list
  END
END ELSE BEGIN
  COMPILE ~nephele/dialogue/fatesp.d~ // only compile this for non-EET installations
END

// Spells

// Good Berries

COPY_EXISTING ~sppr207.spl~ ~override/lkneph1.spl~
WRITE_SHORT 0x1C 4                        
WRITE_LONG 0x34 1
READ_LONG 0x64 "offset_abil"
READ_SHORT  0x68 "num_abil"
READ_ASCII ("%offset_abil%" + 0x04) "bam"            
WRITE_EVALUATED_ASCII 0x3A "%bam%"                
SAY NAME1 @50
SAY NAME2 @50
SAY UNIDENTIFIED_DESC @51
SAY DESC @51
WHILE (0 < "%num_abil%")
BEGIN
WRITE_SHORT (("%offset_abil%" + 0x02) + (("%num_abil%" - 1) * 0x28)) 4
SET "num_abil" = ("%num_abil%" - 1)
END 

// Entangle

COPY_EXISTING ~sppr105.spl~ ~override/lkneph2.spl~
WRITE_SHORT 0x1C 4                        
WRITE_LONG 0x34 1
READ_LONG 0x64 "offset_abil"
READ_SHORT  0x68 "num_abil"
READ_ASCII ("%offset_abil%" + 0x04) "bam"            
WRITE_EVALUATED_ASCII 0x3A "%bam%"                
SAY NAME1 @52                           
SAY NAME2 @52                           
SAY UNIDENTIFIED_DESC @53
SAY DESC @53
WHILE (0 < "%num_abil%")
BEGIN
WRITE_SHORT (("%offset_abil%" + 0x02) + (("%num_abil%" - 1) * 0x28)) 4
SET "num_abil" = ("%num_abil%" - 1)
END

// Zone of Sweet Air

COPY_EXISTING ~sppr318.spl~ ~override/lkneph3.spl~           
WRITE_SHORT 0x1C 4                        
WRITE_LONG 0x34 1
READ_LONG 0x64 "offset_abil"
READ_SHORT  0x68 "num_abil"
READ_ASCII ("%offset_abil%" + 0x04) "bam"            
WRITE_EVALUATED_ASCII 0x3A "%bam%"                
SAY NAME1 @54
SAY NAME2 @54
SAY UNIDENTIFIED_DESC @55
SAY DESC @55
WHILE (0 < "%num_abil%")
BEGIN
WRITE_SHORT (("%offset_abil%" + 0x02) + (("%num_abil%" - 1) * 0x28)) 4
SET "num_abil" = ("%num_abil%" - 1)
END

//Symbol

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
COPY ~nephele/bams/ee~ ~override~
END ELSE BEGIN
COPY ~nephele/bams/bg2~ ~override~
END
COPY ~nephele/items/lk#neph.itm~ ~override/lk#neph.itm~
SAY NAME1 @56
SAY NAME2 @56
SAY DESC @57

BEGIN @58
  REQUIRE_COMPONENT ~setup-nephele.tp2~ ~0~ @59
  REQUIRE_COMPONENT ~setup-haerdalisromance.tp2~ ~0~ @60

COMPILE ~nephele/dialogue/haerdalisromance.d~
EXTEND_BOTTOM ~lk#neph.bcs~ ~nephele/scripts/haerdalisromance.baf~